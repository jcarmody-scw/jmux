set shell := ["bash", "-euo", "pipefail", "-c"]

root := justfile_directory()
bin  := root / "bin"

export CGO_ENABLED := "0"
export VERSION     := env_var_or_default("VERSION", "dev")

# Default: full build
default: build

# Full build: frontend → embed → go binaries
build:
    cd {{root}} && pnpm install
    bash {{root}}/scripts/build.sh

# Build Go binaries only (skip frontend)
build-go:
    bash {{root}}/scripts/build.sh --skip-frontend

# Run tests (Go + JS)
test:
    cd {{root}}/services/gmuxd  && go test ./...
    cd {{root}}/cli/gmux        && go test ./...
    cd {{root}}/packages/adapter && go test ./...
    cd {{root}}/apps/gmux-web   && npx vitest run --passWithNoTests

# Start the dev stack (vite + gmuxd + file watcher)
dev:
    bash {{root}}/scripts/dev-server.sh

# Start the vite dev server against the already-running daemon at :8790 (frontend changes only)
dev-frontend:
    cd {{root}}/apps/gmux-web && npx vite

# Start gmuxd against built binaries
start:
    #!/usr/bin/env bash
    os=$(uname -s | tr '[:upper:]' '[:lower:]')
    exec {{bin}}/gmuxd-${os}-arm64 start

# Install binaries to $(brew --prefix)/bin and register gmuxd as a launchd agent.
# Works on macOS (installs darwin-arm64 build); can also be run on Linux.
install:
    #!/usr/bin/env bash
    set -euo pipefail
    goos=$(go env GOOS)
    goarch=$(go env GOARCH)
    prefix=$(brew --prefix 2>/dev/null || echo /usr/local)
    src_gmux="{{bin}}/gmux-${goos}-${goarch}"
    src_gmuxd="{{bin}}/gmuxd-${goos}-${goarch}"
    if [ ! -f "$src_gmux" ] || [ ! -f "$src_gmuxd" ]; then
      echo "Arch-specific binaries not found: $src_gmux / $src_gmuxd"
      echo "Run 'just build' first."
      exit 1
    fi
    cp "$src_gmux"  "$prefix/bin/gmux"
    cp "$src_gmuxd" "$prefix/bin/gmuxd"
    if command -v codesign >/dev/null 2>&1; then
      codesign --sign - --force "$prefix/bin/gmux"
      codesign --sign - --force "$prefix/bin/gmuxd"
    fi
    if [[ "$goos" == "darwin" ]]; then
      plist="$HOME/Library/LaunchAgents/com.gmuxapp.gmuxd.plist"
      mkdir -p "$HOME/Library/LaunchAgents" "$HOME/.local/state/gmux"
      sed \
        -e "s|\${GMUXD_BIN}|$prefix/bin/gmuxd|g" \
        -e "s|\${HOME}|$HOME|g" \
        -e "s|\${SHELL}|$SHELL|g" \
        -e "s|\${PATH}|$PATH|g" \
        -e "s|\${GMUX_CONFIG_DIR}|${GMUX_CONFIG_DIR:-$HOME/james-agent-workspace/.gmux}|g" \
        "{{justfile_directory()}}/scripts/com.gmuxapp.gmuxd.plist.template" \
        > "$plist"
      echo "Installing launchd agent..."
      launchctl bootout "gui/$(id -u)/com.gmuxapp.gmuxd" 2>/dev/null || true
      # bootout is async — wait for the service to actually disappear before bootstrapping,
      # otherwise launchd returns error 5 (I/O error) on the bootstrap call.
      for i in $(seq 1 20); do
        launchctl print "gui/$(id -u)/com.gmuxapp.gmuxd" >/dev/null 2>&1 || break
        sleep 0.25
      done
      launchctl bootstrap "gui/$(id -u)" "$plist"
      echo "Done. gmux and gmuxd installed to $prefix/bin/ (gmuxd running as launchd agent)"
    else
      echo "Restarting gmuxd..."
      nohup gmuxd restart >/dev/null 2>&1 &
      echo "Done. gmux and gmuxd installed to $prefix/bin/"
    fi

